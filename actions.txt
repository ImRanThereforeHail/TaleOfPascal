// Counts how many times the plate in the first room has been pressed
set plate_1_count = 10

// Quotes are only necessary when the string contains spaces
set message = "The four plates must be pressed in the right order..."
set cursor_x = 0
set cursor_y = 24

set message at 3 = p
// Example for map tile reading
set tile_at_some_pos = tile 0 12
// Example for double pointers
set plate_1_ptr = "plate_1_count"

// Counts how many correct plates have been successfully stepped on in the numbers puzzle
set puzzle_count = 0

// Secret rock >.>
touch 3 3
    set_tile 1 1 0 r

// First room plate
touch 28 6
    set plate_1_count -= 1

    if $$plate_1_ptr > 0
        set_tile 28 5 $plate_1_count _
    else
        set_tile 28 5 " " _
        set_tile 32 6 " " o
        set_tile 28 6 o P

        call print_message

func reset_plates
    set puzzle_count = 0
    set_tile 51 3 1 " "
    set_tile 67 3 2 " "
    set_tile 51 8 3 " "
    set_tile 67 8 4 " "

// Plate 4
touch 67 9
    if $puzzle_count == 0
        set_tile 67 8 4 n
        set puzzle_count += 1

// Plate 2
touch 67 4
    if $puzzle_count == 1
        set_tile 67 3 2 n
        set puzzle_count += 1
    elif $puzzle_count < 1
        call reset_plates

// Plate 1
touch 51 4
    if $puzzle_count == 2
        set_tile 51 3 1 n
        set puzzle_count += 1
    elif $puzzle_count < 2
        call reset_plates

// Plate 3
touch 51 9
    if $puzzle_count == 3
        set puzzle_count += 1
        set_tile 51 8 3 n
        set_tile 19 12 " " o
        set_tile 20 12 " " o
        set_tile 21 12 " " o
        set_tile 22 12 " " o
        set_tile 23 12 " " o
        
        // Clear the hint message
        set message = "                                                      "
        set cursor_x = 0
        call print_message
    elif $puzzle_count < 3
        call reset_plates

// Print text contained in "message" to (cursor_x, cursor_y)
func print_message
    set letter = $message at $cursor_x
    if $letter != ""
        set_tile $cursor_x $cursor_y $letter h
        set cursor_x += 1
        set length = len $message
        call print_message

// Simple addition loop example
func loop_add
    set a += 1
    if $a < 10
        call loop_add
